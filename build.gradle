buildscript {

    ext {
        versions = [
                jackson       : '2.9.5',
                springBoot    : '2.0.1.RELEASE',
                andamio       : '2.3.4',
                springKafka   : '2.1.5.RELEASE',
                spock         : '1.1-groovy-2.4',
                groovy        : '2.4.15',
                jaxb          : '2.3.0',
                geb           : '2.1',
                chromeSelenium: '3.11.0'
        ]

        drivers = ["chromeHeadless"]
    }

    repositories {
        maven { url 'https://artifactory.allegrogroup.com/artifactory/group-allegro/' }
        maven { url 'https://artifactory.allegrogroup.com/artifactory/remote-repos/' }
        mavenCentral()
    }

    dependencies {
        classpath group: 'pl.allegro.tech.build', name: 'axion-gradle-plugin', version: "1.2.2"
        classpath group: 'pl.allegro.tech.phoenix', name: 'phoenix-provisioning-plugin', version: "1.0.12"
    }
}

plugins {
    id 'groovy'
    id 'application'
    id "com.moowork.node" version "1.2.0"
    id 'maven-publish'
    id 'pl.allegro.tech.build.axion-release' version '1.9.3'
}

apply plugin: 'axion'
apply plugin: 'phoenix-provisioning'

scmVersion {
    tag {
        prefix = project.rootProject.name
    }
    versionCreator 'versionWithBranch'
}

sourceCompatibility = 10

project.group = 'pl.allegro.experiments.chi'
project.version = scmVersion.version

repositories {
    maven { url 'https://artifactory.allegrogroup.com/artifactory/group-maven/' }
    mavenCentral()
}

dependencies {
    compile 'org.javers:javers-spring-boot-starter-mongo:3.11.3'
    compile 'pl.allegro.tech.common:andamio-starter-webmvc:' + versions.andamio
    compile group: 'org.springframework.security.oauth.boot', name: 'spring-security-oauth2-autoconfigure', version: versions.springBoot
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: versions.springBoot
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-mongodb', version: versions.springBoot
    compile group: 'org.springframework.session', name: 'spring-session-data-mongodb', version: '2.0.2.RELEASE'
    compile group: 'org.springframework.security', name: 'spring-security-jwt', version: '1.0.8.RELEASE'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-avro', version: versions.jackson
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: versions.jackson
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: versions.jackson
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-parameter-names', version: versions.jackson
    compile group: 'org.springframework.kafka', name: 'spring-kafka', version: versions.springKafka
    compile group: 'pl.allegro.tech.common', name: 'spring-boot-starter-andamio-avro', version: '0.7.1'
    compile group: 'pl.allegro.tech.common', name: 'andamio-endpoint-discovery-spring', version: '0.8.1'
    compile 'com.squareup.okhttp3:okhttp:3.9.1'
    compile 'com.github.slugify:slugify:2.1.12'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'

    //dependencies missing on Java 10 jre
    compile "javax.activation:activation:1.1.1"
    compile "javax.xml.bind:jaxb-api:$versions.jaxb"
    compile "com.sun.xml.bind:jaxb-core:$versions.jaxb"
    compile "com.sun.xml.bind:jaxb-impl:$versions.jaxb"

    testCompile group: 'org.springframework.boot', name: 'spring-boot-properties-migrator', version: versions.springBoot
    testCompile group: 'cglib', name: 'cglib-nodep', version: '3.2.6'
    testCompile group: 'org.objenesis', name: 'objenesis', version: '1.2'
    testCompile group: 'com.github.tomakehurst', name: 'wiremock', version: '2.1.11'
    testCompile group: 'org.spockframework', name: 'spock-core', version: versions.spock
    testCompile group: 'org.codehaus.groovy', name: 'groovy-all', version: versions.groovy
    testCompile group: 'com.jayway.jsonpath', name: 'json-path', version: '2.4.0'
    testCompile group: 'org.spockframework', name: 'spock-spring', version: versions.spock
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot
    testCompile group: 'org.springframework.kafka', name: 'spring-kafka-test', version: versions.springKafka
    testCompile group: 'com.github.fakemongo', name: 'fongo', version: '2.2.0-RC1'

    testCompile group: 'org.gebish', name: 'geb-spock', version: versions.geb
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-chrome-driver', version: versions.chromeSelenium
}

drivers.each { driver ->
    task "${driver}Test"(type: Test) {
        group JavaBasePlugin.VERIFICATION_GROUP

        outputs.upToDateWhen { false }  // Always run tests

        systemProperty "geb.build.reportsDir", reporting.file("geb/$name")
        systemProperty "geb.env", driver
    }
}

mainClassName = 'pl.allegro.experiments.chi.chiserver.AppRunner'

node {
    version = '8.9.1'
    download = true
}

task frontendDeps(type: YarnTask) {
    execOverrides { it.workingDir = './src/main/frontend' }
    args = ['install']
}

task frontendBuild(type: YarnTask) {
    dependsOn frontendDeps
    execOverrides { it.workingDir = './src/main/frontend' }
    args = ['build']
}

processResources {
    dependsOn frontendBuild
    from('public/') {
        into 'public'
    }
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat = 'full'
    }
}

tasks.withType(GroovyCompile) {
    groovyOptions.forkOptions.memoryMaximumSize = '256m'
}

integrationTest {
    systemProperty 'spock.configuration', 'SpockConfiguration.groovy'
    maxHeapSize = "1g"
    jvmArgs '-XX:MaxMetaspaceSize=512m'
    dependsOn drivers.collect { tasks["${it}Test"] }

    testLogging {
        exceptionFormat = 'full'
        //showStandardStreams = true
    }
}

test {
    afterTest { desc, result ->
        logger.quiet "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }

    testLogging {
        exceptionFormat = 'full'
    }
}

axionPublishing {
    repositories {
        allegro {
            url = 'https://artifactory.allegrogroup.com/artifactory/allegro-{}s-local/'
        }
    }
    apply()
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.9'
}
